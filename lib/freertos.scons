Import("env")

if not (rtos_flavor := env["HW_RTOS_FLAVOR"]):
    raise Exception("rtos_flavor not set for current target")

_rtos_dirs = {
    "freertos-cm33-ntz": [
        Dir("FreeRTOS-Kernel/portable/GCC/ARM_CM33/non_secure").srcnode(),
    ],
    "freertos-cm4": [
        Dir("FreeRTOS-Kernel/portable/GCC/ARM_CM4F").srcnode(),
    ],
}

_rtos_sources = {
    "freertos-cm33-ntz": [
        File("FreeRTOS-Kernel/portable/GCC/ARM_CM33_NTZ/non_secure/port.c"),
        File("FreeRTOS-Kernel/portable/GCC/ARM_CM33_NTZ/non_secure/portasm.c"),
    ],
    "freertos-cm4": [
        File("FreeRTOS-Kernel/portable/GCC/ARM_CM4F/port.c"),
    ],
}

env.AppendUnique(
    CPPPATH=[
        Dir("FreeRTOS-Kernel/include").srcnode(),
        *_rtos_dirs[rtos_flavor],
        Dir("FreeRTOS-glue").srcnode(),
    ],
    CCFLAGS=[
        # "-Wno-unused-parameter",
    ],
)


libenv = env.Clone(FW_LIB_NAME="freertos")
libenv.ApplyLibFlags()


sources = libenv.Glob("FreeRTOS-Kernel/*.c", source=True)
sources += _rtos_sources[rtos_flavor]

lib = libenv.StaticLibrary("${FW_LIB_NAME}", sources)
libenv.Install("${LIB_DIST_DIR}", lib)
Return("lib")
